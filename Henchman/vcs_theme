#!/bin/echo Please use source 

print 'Setting up for vcs info themes'
autoload -Uz vcs_info
function precmd() { vcs_info }

# colors!
# You can use names or numbers. I like names.
color_branch='magenta'
color_staged='blue'
color_unstaged='red'
color_clean='green'
color_action='cyan'

format="(%B%F{$color_branch}%b%f%%b"
change_format="(%B%F{$color_branch}%b%f%%b|"
actionformat="(%B%F{$color_branch}%b%f%%b|%F{$color_action}%a%f"
change_actionformat="(%B%F{$color_branch}%b%f%%b:%F{$color_action}%a%f|"
not_smart='[%s]-'
not_smart_mark='%#'
changes='%u%c'
no_changes='%{%}'
# default repo format:
zstyle ':vcs_info:*' max-exports 3
zstyle ':vcs_info:*' formats $not_smart$format $not_smart_mark $changes
zstyle ':vcs_info:*' actionformats $not_smart$actionformat $not_smart_mark $changes
zstyle ':vcs_info:*' nvcsformats '' $not_smart_mark

# smart marks:
# We specify width, since some terminals get upset otherwise
sm_bzr='%2{⚯ %}'
sm_fossil='%2{⌘ %}'
sm_git='%1{±%}'
sm_hg='%1{☿%}'
sm_svn='%1{‡%}'

zstyle ':vcs_info:bzr:*' formats $format $sm_bzr $no_changes
zstyle ':vcs_info:bzr:*' actionformats $actionformat $sm_bzr $no_changes

zstyle ':vcs_info:fossil:*' formats $format $sm_fossil $no_changes
zstyle ':vcs_info:fossil:*' actionformats $actionformat $sm_fossil $no_changes

zstyle ':vcs_info:git:*' formats $change_format $sm_git $changes
zstyle ':vcs_info:git:*' actionformats $change_actionformat $sm_git $changes

zstyle ':vcs_info:hg:*' formats $format $sm_hg $no_changes
zstyle ':vcs_info:hg:*' actionformats $actionformat $sm_hg $no_changes

zstyle ':vcs_info:svn:*' formats $format $sm_svn $no_changes
zstyle ':vcs_info:svn:*' actionformats $actionformat $sm_svn $no_changes

# Make %u and %c useful:
zstyle ':vcs_info:*' check-for-changes true

# For %u and %c to work for mercurial, extra steps are required:
zstyle ':vcs_info:hg:*' get-revision true
zstyle ':vcs_info:hg:*' use-simple false # default, but why trust that?

# Make use of mercurial's local revision number
zstyle ':vcs_info:hg:*' hgrevformat '%r:%8.8h' # this is %i in formats
zstyle ':vcs_info:hg:*' hgrevformat '%r' # this is %i in formats
zstyle ':vcs_info:hg:*' branchformat '%b(%r)'
zstyle ':vcs_info:hg:*' branchformat '%r:%b'

# These need to have spaces, since they appear double-wide, except for new file
# Only in some fonts are they that way
#   staged:    blue ✚
# unstaged:     red ●
# all good:   green ✔
# new file: default …
#    ahead: ↑
#   behind: ↓
# diverged: ↕
zstyle ':vcs_info:*' stagedstr "%F{$color_staged}%2{✚ %}%f"
zstyle ':vcs_info:*' unstagedstr "%F{$color_unstaged}%2{● %}%f"

# How to integrate checking for pushes/pulls server side? what hook?

# To allow the green checkmarks to work, we need to define vcs_info_2_ to be a nonprinting
# non-null.  %{%} works for this purpos. Looks ugly, but it's the option
PS1='%F{5}[%F{2}%n%F{5}]%f %F{3}%3~%f ${vcs_info_msg_0_:+${vcs_info_msg_0_}${vcs_info_msg_2_:-%F{green\}%1{✔%\}%f})}%B${vcs_info_msg_1_}%b '


# % escapes:
# %a (action)
# %b (branch: branch name)
# %b (branch name)
# %c (changes staged)
# %c (patch: number of unapplied patches)
# %g (patch: names of active mq guards)
# %G (patch: number of active mq guards)
# %h (hgrev: 40 byte hash)
# %i (identifier or revision)
# %m (misc replacement [git/hg patch])
# %n (patch: number of applied patches)
# %p (patch: top patch applied [applied-string])
# %r (branch: current revision)
# %r (hgrev: local revision)
# %R (repository base directory)
# %r (repository name)
# %S (subdirectory within repository)
# %s (vcs in use)
# %u (patch: number of unapplied patches [unapplied-string])
# %u (unstaged changes)
#
# %) )
# %# %(!.#.%%)
# %% %
# %B start bold
# %b stop bold
# %C trailing commponent of $PWD
# %c trailing commponent of $PWD with ~ contraction
# %D date (yy-mm-dd)
# %D %D{string} strftime
# %d $PWD
# %E clear to end of line
# %F start foreground color
# %f stop foreground color
# %G glitch - specify width in %{...%}
# %h history number
# %! history number
# %I current line number of current file $PS4
# %i current line number $PS4
# %( %(...) if/then/else
# %j number of jobs
# %K start background color
# %k stop background color
# %< %<...< left truncation
# %l line/tty
# %L $SHLVL
# %M FQDN
# %m hostname up to first .
# %N name of current script / $0
# %n $USERNAME
# %/ $PWD
# %~ $PWD, with ~ contraction
# %> %>...> right truncation
# %S start standout
# %s stop standout
# %{ start literal escape
# %? status code
# %_ status of parser (if/for) $PS2
# %} stop literal escape
# %* time (24 hours, with seconds)
# %@ time (am/pm)
# %. trailing commponent of $PWD
# %T time (24hr)
# %t time (am/pm)
# %U start underline
# %u stop underline
# %v $psvar
# %w date in day-dd
# %W date mm/dd/yy
# %[ %[x...] left(x=<) or right (x=>) truncation
# %x name of file with current source code executed
# %y tty w/o /dev
