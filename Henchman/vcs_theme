#!/bin/echo Please use source 

print 'Setting up for vcs info themes'
autoload -Uz vcs_info
function precmd() { vcs_info }

format='[%b]'
actionformat='[%b|%a]'
not_smart='(%s)-'
not_smart_mark='%#'
changes='%u%c'
# default repo format:
zstyle ':vcs_info:*' max-exports 3
zstyle ':vcs_info:*' formats $not_smart$format $not_smart_mark $changes
zstyle ':vcs_info:*' actionformats $not_smart$actionformat $not_smart_mark $changes
zstyle ':vcs_info:*' nvcsformats '' $not_smart_mark

# smart marks:
sm_bzr='%2{⚯ %}'
sm_fossil='%2{⌘ %}'
sm_git='%1{±%}'
sm_hg='%1{☿%}'
sm_svn='%1{‡%}'

zstyle ':vcs_info:bzr:*' formats $format $sm_bzr $changes
zstyle ':vcs_info:bzr:*' actionformats $actionformat $sm_bzr $changes

zstyle ':vcs_info:fossil:*' formats $format $sm_fossil $changes
zstyle ':vcs_info:fossil:*' actionformats $actionformat $sm_fossil $changes

zstyle ':vcs_info:git:*' formats $format $sm_git $changes
zstyle ':vcs_info:git:*' actionformats $actionformat $sm_git $changes

zstyle ':vcs_info:hg:*' formats $format $sm_hg $changes
zstyle ':vcs_info:hg:*' actionformats $actionformat $sm_hg $changes

zstyle ':vcs_info:svn:*' formats $format $sm_svn $changes
zstyle ':vcs_info:svn:*' actionformats $actionformat $sm_svn $changes

# Make %u and %c useful:
zstyle ':vcs_info:*' check-for-changes true

# For %u and %c to work for mercurial, extra steps are required:
zstyle ':vcs_info:hg:*' get-revision true
zstyle ':vcs_info:hg:*' use-simple false # default, but why trust that?

# Make use of mercurial's local revision number
zstyle ':vcs_info:hg:*' hgrevformat '%r:%8.8h' # this is %i in formats
zstyle ':vcs_info:hg:*' branchformat '%b(%r)'

# These need to have spaces, since they appear double-wide, except for new file
#   staged:    blue ✚
# unstaged:     red ●
# all good:   green ✔
# new file: default …
zstyle ':vcs_info:*' stagedstr '%F{blue}%2{✚ %}%f'
zstyle ':vcs_info:*' unstagedstr '%F{red}%2{● %}%f'

# How to integrate checking for pushes/pulls server side? what hook?

PS1='%F{5}[%F{2}%n%F{5}] %F{3}%3~ %F{4}${vcs_info_msg_0_:+${vcs_info_msg_0_}${vcs_info_msg_2_:-%F{green\}%2{✔ %\}}}%f${vcs_info_msg_1_} '
